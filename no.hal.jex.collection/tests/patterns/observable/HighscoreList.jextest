test patterns.observable.HighscoreList

import patterns.observable.HighscoreList

instance highscoreList = new HighscoreList(3)

sequence constructor {
	-->
	state highscoreList {
		size() == 0
	}
}

sequence addElementSimple {
	-- highscoreList.addResult(5) -->
	state {
		highscoreList ?= #[5]
	}
	-- highscoreList.addResult(6) -->
	state {
		highscoreList ?= #[5, 6]
	}
	-- highscoreList.addResult(2) -->
	state {
		highscoreList ?= #[2, 5, 6]
	}
}

sequence addElementMoreThanMax {
	-- highscoreList.addResult(5), highscoreList.addResult(6), highscoreList.addResult(2) -->
	state {
		highscoreList ?= #[2, 5, 6]
	}
	-- highscoreList.addResult(3) -->
	state {
		highscoreList ?= #[2, 3, 5]
	}
	-- highscoreList.addResult(7) -->
	state {
		highscoreList ?= #[2, 3, 5]
	}
}

sequence addElementDuplicate {
	-- highscoreList.addResult(5), highscoreList.addResult(6), highscoreList.addResult(2) -->
	state {
		highscoreList ?= #[2, 5, 6]
	}
	-- highscoreList.addResult(2) -->
	state {
		highscoreList ?= #[2, 2, 5]
	}
}

val int pos1 = -1, int pos2 = -1

method void addResult(int element, int pos) {
	pos1 = pos
	highscoreList.addResult(element)
	assertEquals(pos1, pos2)
}

sequence listListenerSimple {
	-- highscoreList.addListListener([list, pos | pos2 = pos]),
	addResult(5, 0) -->
	state {
		highscoreList ?= #[5]
	}
	-- addResult(6, 1) -->
	state {
		highscoreList ?= #[5, 6]
	}
	-- addResult(2, 0) -->
	state {
		highscoreList ?= #[2, 5, 6]
	}
}

sequence listListenerMoreThanMax {
	-- highscoreList.addListListener([list, pos | pos2 = pos]),
	highscoreList.addResult(5), highscoreList.addResult(6), highscoreList.addResult(2) -->
	state {
		highscoreList ?= #[2, 5, 6]
	}
	-- addResult(3, 1) -->
	state {
		highscoreList ?= #[2, 3, 5]
	}
	-- pos2 = -1, addResult(7, -1) -->
	state {
		highscoreList ?= #[2, 3, 5]
	}
}

sequence listListenerDuplicate {
	-- highscoreList.addListListener([list, pos | pos2 = pos]),
	highscoreList.addResult(5), highscoreList.addResult(6), highscoreList.addResult(2) -->
	state {
		highscoreList ?= #[2, 5, 6]
	}
	-- addResult(2, 1) -->
	state {
		highscoreList ?= #[2, 2, 5]
	}
}

method operator_assertEquals(HighscoreList highscoreList, int[] elements) {
	elements.length ?= highscoreList.size()
	var i = 0
	for (element : elements) {
		element ?= highscoreList.getElement(i)
		i++
	}
}
